buildscript {
  repositories {
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }

  dependencies {
    classpath 'gradle.plugin.net.minecrell:licenser:0.4.1'
  }
}



subprojects {
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'signing'
  apply plugin: 'net.minecrell.licenser'

  group 'org.spongepowered'
  version '3.7-SNAPSHOT'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-path', '-Xlint:-serial', '-parameters']
    options.deprecation = true
    options.encoding = 'UTF-8'
  }

  java {
    withJavadocJar()
    withSourcesJar()
  }

  javadoc {
    options.links(
      'http://google.github.io/guava/releases/21.0/api/docs/'
    )
  }

  license {
    header rootProject.file('LICENSE_HEADER')
    include '**/*.java'
    include '**/*.kt'
    newLine false
  }

  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
  }

  dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.2.0'
    testImplementation 'org.junit-pioneer:junit-pioneer:0.1.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
  }

  test {
    useJUnitPlatform()
  }


  publishing {
    publications {
      maven(MavenPublication) {
        from components.java

        pom {
          description 'A simple configuration library for Java applications that can handle a variety of formats and provides a node-based data structure able to handle a wide variety of configuration schemas'
          name = project.name
          url = 'https://github.com/SpongePowered/configurate/'

          inceptionYear = '2014'

          developers {
            developer {
              name = 'zml'
              email = 'zml@aoeu.xyz'
            }
          }

          issueManagement {
            system = 'GitHub Issues'
            url = 'https://github.com/SpongePowered/configurate/issues'
          }

          licenses {
            license {
              name = 'Apache License, Version 2.0'
              url = 'https://opensource.org/licenses/Apache-2.0'
            }
          }

          scm {
            connection = 'scm:git@github.com:SpongePowered/configurate.git'
            developerConnection = 'scm:git@github.com:SpongePowered/configurate.git'
            url = 'https://github.com/SpongePowered/configurate/'
          }

        }
      }
    }

    if (project.hasProperty('spongeRepo') && project.hasProperty('spongeUsername') && project.hasProperty('spongePassword')) {
        repositories {
          maven {
            name = 'spongeRepo'
            url = spongeRepo
            credentials {
              username = spongeUsername
              password = spongePassword
            }
          }
        }
      }
  }

  signing {
    useGpgCmd()
    sign publishing.publications.maven
  }

  tasks.withType(Sign) {
    onlyIf { !version.endsWith("-SNAPSHOT") }
  }
}
